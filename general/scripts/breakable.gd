@tool
@icon("res://general/icons/breakable.svg")
class_name Breakable
extends Node2D

signal destroyed
signal damage_taken

@export var hp : float = 3
@export var fixed_hit_count : bool = false

@export_category( "Particles" )
@export var emission_offset : Vector2 = Vector2.ZERO
@export var hit_particles : Array [HitParticleSettings]
@export var destroyed_particles : Array [HitParticleSettings]

@export_category( "Audio" )
@export var hit_audio : AudioStream = preload("uid://c4bkgbfnwiuwd")
@export var destroy_audio : AudioStream = preload("uid://d3iua3acq8eum")

func _ready() -> void:
	if Engine.is_editor_hint():
		return
	
	for c in get_children():
		if c is DamageArea:
			c.damage_taken.connect( _on_damage_taken)
			
func _on_damage_taken( attack_area : AttackArea ) -> void:
	if fixed_hit_count:
		hp -= 1
	else:
		hp -= attack_area.damage
		
	var dir : Vector2 = Vector2(1, -1)
	var pos = global_position + emission_offset
	if attack_area.global_position.x > global_position.x:
		dir.x *= -1
		
	if hp > 0:
		damage_taken.emit()
		Audio.play_spatial_sound(hit_audio, pos)
		for p in hit_particles:
			VisualEffects.hit_particles(pos, dir, p)
	else:
		destroyed.emit()
		Audio.play_spatial_sound(destroy_audio, pos)
		for p in destroyed_particles:
			VisualEffects.hit_particles(pos, dir, p)
		clear_collisions()
		var tween = create_tween()
		tween.tween_property(self, "modulate", Color(modulate, 0), 0.4)
		await tween.finished
		queue_free()

func clear_collisions() -> void:
	for c in get_children():
		if c is StaticBody2D:
			c.queue_free()

func _get_configuration_warnings() -> PackedStringArray:
	if _check_for_damage_area() == false:
		return ["Requires a DamageArea node"]
	else:
		return []
		
func _check_for_damage_area() -> bool:
	for c in get_children():
		if c is DamageArea:
			return true
	return false
