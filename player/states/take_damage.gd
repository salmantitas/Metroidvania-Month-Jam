class_name PlayerStateTakeDamage
extends PlayerState

@export var move_speed : float = 100
@export var invulnerable_duration : float = 0.5
var time : float = 0
var dir : float = 1.0
@onready var damage_area: DamageArea = %DamageArea
const HURT_AUDIO = preload("uid://dh5hswax7uu0r")

func init() -> void:
	damage_area.damage_taken.connect( _on_damage_taken )

func enter() -> void:
	player.animation_player.play("take_damage")
	time = player.animation_player.current_animation_length
	damage_area.make_invulnerable( invulnerable_duration )
	Audio.play_spatial_sound(HURT_AUDIO, player.global_position)
	VisualEffects.camera_shake(1)
	pass
	
func exit() -> void:
	pass

# Takes an input and determines which state to change to
func handle_input( _event : InputEvent ) -> PlayerState:
	return null

func process(delta: float) -> PlayerState:
	time -= delta
	if time <= 0:
		if player.hp <= 0:
			return death
		return idle
	return null
	
func physics_process(_delta: float) -> PlayerState:
	player.velocity.x = move_speed * dir
	return null

func _on_damage_taken( attack_area : AttackArea ) -> void:
	if player.current_state == death:
		return
		
	player.change_state(self)
	if attack_area.global_position.x < player.global_position.x:
		dir = 1.0
	else:
		dir = -1.0
