extends Node

const CONFIG_FILE_PATH = "user://settings.cfg"
const SLOTS : Array[ String ] = [
	"save_01", "save_02", "save_03"
]

var current_slot : int = 0
var save_data : Dictionary  = {}
var discovered_areas : Array = []
var persistent_data : Dictionary = {}

const new_game_path_test : String = "uid://b2so5wjsaqq0x"
const new_game_path_mvm : String = "uid://tv46cj7vcfkf"

const new_game_scene_path : String = new_game_path_mvm

func _ready() -> void:
	SceneManager.scene_entered.connect( _on_scene_entered )
	load_configuration()

func _unhandled_input(event: InputEvent) -> void:
	# DEBUG
	if OS.is_debug_build():
		if event is InputEventKey and event.pressed:
			if event.keycode == KEY_F5:
				save_game()
			elif event.keycode == KEY_F6:
				load_game( current_slot )
			elif event.keycode == KEY_1:
				current_slot = 0
			elif event.keycode == KEY_2:
				current_slot = 1
			elif event.keycode == KEY_3:
				current_slot = 2
	# END DEBUG
	
func create_new_game_save( slot : int) -> void:
	current_slot = slot
	discovered_areas.clear()
	persistent_data.clear()
	var new_game_scene : String = new_game_scene_path
	discovered_areas.append(new_game_scene)
	save_data = {
		"scene_path" : new_game_scene,
		"x" : 128.0,
		"y" : 344,
		"hp" : 20,
		"max_hp" : 20,
		"dash" : false,
		"double_jump" : false,
		"ground_slam" : false,
		"morph" : false,
		"discovered_areas": discovered_areas,
		"persistent_data": persistent_data
	}
	var save_file = FileAccess.open(get_file_name( current_slot ), FileAccess.WRITE)
	save_file.store_line(JSON.stringify(save_data))
	save_file.close()
	
	load_game(slot)

func save_game() -> void:
	var player : Player = get_tree().get_first_node_in_group( "Player" )
	save_data = {
		"scene_path" : SceneManager.current_scene_uid,
		"x" : player.global_position.x,
		"y" : player.global_position.y,
		"hp" : player.hp,
		"max_hp" : player.max_hp,
		"dash" : player.dash,
		"double_jump" : player.double_jump,
		"ground_slam" : player.ground_slam,
		"morph" : player.morph,
		"discovered_areas": discovered_areas,
		"persistent_data": persistent_data
	}
	var save_file = FileAccess.open(get_file_name( current_slot ), FileAccess.WRITE)
	save_file.store_line(JSON.stringify(save_data))

func load_game( slot : int ) -> void:
	if not FileAccess.file_exists(get_file_name( current_slot )):
		return

	current_slot = slot
		
	var save_file = FileAccess.open(get_file_name( current_slot ), FileAccess.READ)
	save_data = JSON.parse_string( save_file.get_line() )
	
	persistent_data = save_data.get( "persistent_data", {})
	discovered_areas = save_data.get( "discovered_areas", {})
	var scene_path : String = save_data.get( "scene_path", new_game_scene_path)
	SceneManager.transition_scene(scene_path, "", Vector2.ZERO, "top")
	
	# wait until the scene is loaded before setting up the player
	await SceneManager.new_scene_ready
	
	setup_player()

func setup_player() -> void:
	var player : Player = null
	
	while not player:
		player = 	get_tree().get_first_node_in_group( "Player" )
		await get_tree().process_frame	
	
	player.max_hp = save_data.get( "max_hp", 20)
	player.hp = save_data.get( "hp", 20)
	player.dash = save_data.get( "dash", false)
	player.double_jump = save_data.get( "double_jump", false)
	player.ground_slam = save_data.get( "ground_slam", false)
	player.morph = save_data.get( "morph", false)
	
	player.global_position = Vector2(
		save_data.get("x", 0),
		save_data.get("y", 0)
	)

func get_file_name( slot: int) -> String:
	return "user://" + SLOTS[slot] + ".sav"

func save_file_exists (slot : int) -> bool:
	return FileAccess.file_exists( get_file_name(slot) )

func is_area_discovered( scene_uid : String) -> bool:
	return discovered_areas.has(scene_uid)

func _on_scene_entered( scene_uid : String ) -> void:
	if discovered_areas.has(scene_uid):
		pass
	else:
		discovered_areas.append(scene_uid)

func save_configuration() -> void:
	var config := ConfigFile.new()
	config.set_value("audio", "music", AudioServer.get_bus_volume_linear(2))
	config.set_value("audio", "sfx", AudioServer.get_bus_volume_linear(3))
	config.set_value("audio", "ui", AudioServer.get_bus_volume_linear(4))
	config.save( CONFIG_FILE_PATH )

func load_configuration() -> void:
	var config := ConfigFile.new()
	var err = config.load(CONFIG_FILE_PATH)
	
	if err != OK:
		AudioServer.set_bus_volume_linear(2, 0.6)
		AudioServer.set_bus_volume_linear(3, 0.4)
		AudioServer.set_bus_volume_linear(4, 0.2)
		return
	
	AudioServer.set_bus_volume_linear(2, config.get_value("audio", "music", 0.6))
	AudioServer.set_bus_volume_linear(3, config.get_value("audio", "sfx", 0.4))
	AudioServer.set_bus_volume_linear(4, config.get_value("audio", "ui", 0.2))
